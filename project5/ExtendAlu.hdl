// This file is part of nand2tetris, as taught in The Hebrew University,
// and was written by Aviv Yaish, and is published under the Creative 
// Common Attribution-NonCommercial-ShareAlike 3.0 Unported License 
// https://creativecommons.org/licenses/by-nc-sa/3.0/

// The inputs of the extended ALU are instruction[9], x[16], y[16].
// The "ng" and "zr" output pins behave the same as in the regular ALU.
// The "out" output is defined as follows:
// If instruction[8]=1 and instruction[7]=1 the output is identical to the 
// regular ALU, where:
// instruction[5]=zx, instruction[4]=nx, ..., instruction[0]=no
// Else, if instruction[8]=0 and instruction[7]=1 the output is a shift:
// - If instruction[4] == 0, the input "y" will be shifted, otherwise "x", .
// - If instruction[5] == 0, the shift will be a right-shift, otherwise left.
// - All other inputs are undefined.

CHIP ExtendAlu {
     IN x[16], y[16], instruction[9];
     OUT out[16], zr, ng;
     
     PARTS:

     ALU(x=x, y=y, zx=instruction[5], nx=instruction[4], zy=instruction[3], ny=instruction[2], f=instruction[1], no=instruction[0], out=out1, zr=zr1, ng=ng1);

     Mux16(a=y, b=x, sel=instruction[4], out=res1);
     ShiftLeft(in=res1, out=out2);
     ShiftRight(in=res1, out=out3);
     Mux16(a=out3, b=out2, sel=instruction[5], out=res4, out[0..7]=outleft, out[8..15]=outright, out[15]=outlast);
    
     Mux16(a=res4, b=out1, sel=instruction[8], out=out);

     Or8Way(in=outleft, out=out11);
     Or8Way(in=outright, out=out12);
     Or(a=out11, b=out12, out=out13);
     Not(in=out13, out=zr2);
     And(a=outlast, b=outlast, out=ng2);
    
     Mux(a=zr2, b=zr1, sel=instruction[8], out=zr);
     Mux(a=ng2, b=ng1, sel=instruction[8], out=ng);
    
    
}
